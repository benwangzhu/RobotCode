&ACCESS RVP
&COMMENT UnStack Main
&REL 9
&PARAM EDITMASK = *
&PARAM TEMPLATE = C:\KRC\Roboter\Template\vorgabe
&PARAM DISKPATH = KRC:\R1\Program\SbtComm010Stack
DEF UnStackMain( )
;FOLD INI;%{PE}
  ;FOLD BASISTECH INI
    GLOBAL INTERRUPT DECL 3 WHEN $STOPMESS==TRUE DO IR_STOPM ( )
    INTERRUPT ON 3 
    BAS (#INITMOV,0 )
  ;ENDFOLD (BASISTECH INI)
  ;FOLD USER INI
    ;Make your modifications here

  ;ENDFOLD (USER INI)
;ENDFOLD (INI)

;FOLD Config
Comm010Config()
;ENDFOLD
;FOLD SbtComm010.Init IN Mode: UNSTACK ;%{PE}
Comm010WorkMode = 2
ProcessComm010Init()
;ENDFOLD
;FOLD CHECK INIT
if Comm010ResultId <> COMM010_RESULT_OK then  
   RETURN
endif
;ENDFOLD
;FOLD SbtComm010.ReqData ;%{PE}
ProcessComm010Data()
;ENDFOLD
loop
;FOLD WAIT FOR NEW TASK
CONTINUE
wait for Comm010ResultFlg
if Comm010ResultId <> COMM010_RESULT_OK then  
   EXIT
endif
Comm010CopyTable = Comm010SensorTable
;ENDFOLD
;FOLD PICK
SWITCH Comm010CopyTable.Area
CASE "'H02'"
UnStackPick02()
CASE "'H03'"
UnStackPick03()
CASE "'H04'"
UnStackPick04()
CASE "'H05'"
UnStackPick05()
DEFAULT
Offset = 0
str_clear_(Msg[])
swrite(Msg[], State, Offset, "Unknow Area Id To Pick![Mode:%s, TaskId:%d]", "UNSTACK", Comm010CopyTable.TaskNum)
log_error_(Msg[])
ENDSWITCH
;ENDFOLD       
;FOLD PLACE
UnStackPlace01()
;ENDFOLD
endloop
END
