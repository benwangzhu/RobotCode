&ACCESS RVO
&REL 54
&COMMENT Conveyor Pick04
&PARAM DISKPATH = KRC:\R1\Program\SecondSortMain
def second_pkcv04()
;***********************************************************
;
; Copyright 2018 - 2024 speedbot All Rights reserved.
;
; File Name: second_pkcv03.src
;
; Description:
;   Language             ==   Krl for KUKA ROBOT
;   Date                 ==   2022 - 06 - 27
;   Modification Data    ==   2024 - 10 - 25
;
; Author: speedbot
;
; Version: 1.1
;***********************************************************

;FOLD INT
;FOLD basistech ini
global interrupt decl 3 when $stopmess == true do ir_stopm()
interrupt on 3
bas (#initmov,0 )
;ENDFOLD (basistech ini)
;FOLD user ini
;Make your modifications here
;ENDFOLD (user ini)
;ENDFOLD (ini)

;FOLD Basic.Frame OUT Frame Dir: ToolUpOffs, IN x: 0, IN y: 0, IN z: -150, IN a: 0 deg, IN b: 0 deg, IN c: 0 deg ;%{PE}
ToolUpOffs = set_frame_(0, 0, -150, 0, 0, 0)
;ENDFOLD
;FOLD Basic.Frame OUT Frame Dir: ToolRUpOffs, IN x: 0, IN y: 0, IN z: -150, IN a: 0 deg, IN b: 0 deg, IN c: 0 deg ;%{PE}
ToolRUpOffs = set_frame_(0, 0, -150, 0, 0, 0)
;ENDFOLD
;FOLD Basic.Frame OUT Frame Dir: ToolUpTrans, IN x: 0, IN y: 0, IN z: 0, IN a: 0 deg, IN b: 0 deg, IN c: 0 deg ;%{PE}
ToolUpTrans = set_frame_(0, 0, 0, 0, 0, 0)
;ENDFOLD

;FOLD SKIP QUEUE
continue
if $flag[FLAG_CONV_QUE_ACQ[4]] then  
    
    ; Skip queue retrieval when queue already retrieved
    goto DAT_DIST
endif
;ENDFOLD

;FOLD SecondSort.GetQueue IN Conveyer Id: LinConveyor4, IN SleepTime: 100 ms, OUT Status: Status ;%{PE}
second_getq(4, 100, Status)
;ENDFOLD
if Status <> OK then  
    goto SKIP_PKCV
endif

; Successfully retrieved the queue
continue
$flag[FLAG_CONV_QUE_ACQ[4]] = true
PartData = CurQueue[4]
PartData.PkcvOfs.X = CurQueue[4].PkcvOfs.X + PkCvCfg[4].ManualZerOfs.x
PartData.PkcvOfs.y = CurQueue[4].PkcvOfs.y + PkCvCfg[4].ManualZerOfs.y
PartData.PkcvOfs.z = CurQueue[4].PkcvOfs.z + PkCvCfg[4].ManualZerOfs.z
DAT_DIST:

;FOLD SecondSort.CalcWaitPos IN Conveyer Id: LinConveyor4, IN Ref Point Dir: PickPos, IN Distance: 300, OUT Wait Point Dir: WaitPos, OUT Pkcv Dicision: PkcvDicision ;%{PE}
second_wpos(4, XPickPos, 300, XWaitPos , PkcvDicision)
;ENDFOLD
if PkcvDicision == #DCSN_WAIT then
    ;
    ; The component has not yet reached the upstream boundary
    goto SKIP_PKCV
endif
if PartData.TaskId < 0 then  
    ;
    ; Do not pick up
    goto ACK_FAIL
endif
if PkcvDicision == #DCSN_SKIP then
    ; 
    ; Has moved past the downstream boundary of the conveyor
    ; gripper abandoned pickup
    goto ACK_FAIL
endif

servo01_ctrl_(#MOVING, PartData.ServoDist)

; Cannot track the conveyor line in T1 mode
continue
wait for not $t1

ToolUpOffs.a = PartData.PkcvOfs.a * (-1)
ToolRUpOffs.a = PartData.PkcvOfs.a * (-1)
ToolUpTrans = ToolUpOffs
PartData.PkcvOfs.a = 0.0
    
;FOLD SecondSort.MoveToWaitPos IN Conveyer Id: LinConveyor4, IN Pkcv Dicision: PkcvDicision, IN Wait Point Dir: WaitPos, IN ToolOffs: ToolUpOffs, IN Limit: WaitPosLimit, OUT Status: Status ;%{PE}
second_mov_wpos(4, PkcvDicision, XWaitPos, ToolUpOffs, WaitPosLimit, Status)
;ENDFOLD
if Status <> OK then  
    goto ACK_FAIL
endif

continue
$advance = 1
; Activate the conveyor line tracking coordinate system
bas(#base, CONV_BASE_INDEX[4])
bas(#tool, 1)

;FOLD Part Rz >= 170
if (abs(ToolUpOffs.a) >= 170) and (PkcvDicision == #DCSN_PICK) then
   ToolUpTrans = ToolUpOffs
   ToolUpTrans.A = ToolUpTrans.A / 2

;FOLD SecondSort.SyncLin IN Sync Dir: PickPos, IN Queue Data: PartData, IN ToolOffs: ToolUpTrans, IN Sync Vel: 5 m/s, IN Sync Apo: 100, IN Sync Acc: 50 m/s^ ;%{PE}
MoveDLin(ufrm_offs_(XPickPos, PartData.PkcvOfs.X, PartData.PkcvOfs.Y, PartData.PkcvOfs.Z) : ToolUpTrans, 5, 100, , , 50)
;ENDFOLD
endif
;ENDFOLD

;FOLD SecondSort.SyncLin IN Sync Dir: PickPos, IN Queue Data: PartData, IN ToolOffs: ToolUpOffs, IN Sync Vel: 5 m/s, IN Sync Apo: 100, IN Sync Acc: 50 m/s^ ;%{PE}
MoveDLin(ufrm_offs_(XPickPos, PartData.PkcvOfs.X, PartData.PkcvOfs.Y, PartData.PkcvOfs.Z) : ToolUpOffs, 5, 100, , , 50)
;ENDFOLD

ToolUpOffs.z = 0.0

;FOLD SecondSort.SyncLin IN Sync Dir: PickPos, IN Queue Data: PartData, IN ToolOffs: ToolUpOffs, IN Sync Vel: 5 m/s, IN Sync Apo: 1, IN Sync Acc: 50 m/s^ ;%{PE}
MoveDLin(ufrm_offs_(XPickPos, PartData.PkcvOfs.X, PartData.PkcvOfs.Y, PartData.PkcvOfs.Z) : ToolUpOffs, 5, 1, , , 50)
;ENDFOLD

mag_tool_forc_ctrl_(#ON, 500)

;FOLD SecondSort.SyncLin IN Sync Dir: PickPos, IN Queue Data: PartData, IN ToolOffs: ToolRUpOffs, IN Sync Vel: 5 m/s, IN Sync Apo: 100, IN Sync Acc: 50 m/s^ ;%{PE}
MoveDLin(ufrm_offs_(XPickPos, PartData.PkcvOfs.X, PartData.PkcvOfs.Y, PartData.PkcvOfs.Z) : ToolRUpOffs, 5, 100, , , 50)
;ENDFOLD

; Pickup successful
ACK_SUCC:

if PartData.Num == 1 then
    conv_part_nbr[conv_get_machine_index(4)] = 1
    FirstPicking[4] = true
else
    conv_part_nbr[conv_get_machine_index(4)] = 0
endif
continue
$flag[FLAG_CONV_QUE_ACQ[4]] = false
GlbPartData = CurQueue[4]
continue
$flag[FLAG_CONV_PKSU] = true

;FOLD SecondSort.Feedback IN Conveyer Id: LinConveyor4, IN Pick State: Success ;%{PE}
second_fdbk(4, #PICK_SUCC)
;ENDFOLD
goto SKIP_PKCV

; Pickup failed
ACK_FAIL:
if PartData.Num == 1 then
    Count = conv_delete_wps(4, 1, ErrNbr)
    conv_part_nbr[conv_get_machine_index(4)] = 1
    IF FirstPicking[3] == true then
      conv_part_nbr[conv_get_machine_index(4)] = 1
    else
      conv_part_nbr[conv_get_machine_index(4)] = 0
    endif
endif
continue
$flag[FLAG_CONV_QUE_ACQ[1]] = false

;FOLD SecondSort.Feedback IN Conveyer Id: LinConveyor4, IN Pick State: Fail ;%{PE}
second_fdbk(4, #PICK_FAIL)
;ENDFOLD
RETURN

SKIP_PKCV:
continue
$advance = 3
end

;FOLD CONV_QUIT
def conv_quit(Z_CONV_NBR:IN, B_STOP_AFTER_LEAVING:IN)
int Z_CONV_NBR
bool B_STOP_AFTER_LEAVING
int Z_ADVANCE_OLD

Z_ADVANCE_OLD=$ADVANCE

if (B_QUIT_BECAUSE_EMS OR B_QUIT_BECAUSE_MAX_DIST) then
    ;********************************************
    ;Strategy for the robot to leave the conveyor
    ;if there was an EMS or MAX_DIST reached
    ;while synchronising.
    ;********************************************
    switch z_conv_nbr
    case 1
    $base=$nullframe
    lin_rel {z 0.0}
    case 2
    $base=$nullframe
    lin_rel {z 0.0}
    case 3
    $base=$nullframe
    lin_rel {z 0.0}
    case 4
    $base=$nullframe
    lin_rel {z 0.0}
    case 5
    $base=$nullframe
    lin_rel {z 0.0}
    endswitch
    
    if B_STOP_AFTER_LEAVING then
    ;*****************************
    ;* Robot Stopped because of  *
    ;* error while synchronising *
    ;* EMS or MAX_DIST reached   *
    ;*****************************
    loop
        halt
    endloop
    endif
endif
$advance=Z_ADVANCE_OLD
end
;ENDFOLD