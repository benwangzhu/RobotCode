program head_init
--***********************************************************
--
-- Copyright 2018 - 2025 speedbot All Rights reserved.
--
-- file Name: head_init
--
-- Description:
--   Language             ==   Karel for FANUC ROBOT
--   Date                 ==   2025 - 04 - 29
--   Modification Data    ==   2025 - 05 - 06
--
-- Author: 
--
-- Version: 1.0
--***********************************************************
--
%comment = 'HeadInit'
%nolockgroup
%nobusylamp
%nopauseshft

%include include/lib_logs_t
%include include/lib_tpe_t
%include include/lib_inst_t
%include include/lib_socket_t
%include include/lib_transform_t
%include include/lib_math_t
%include sbt_comm012_t
%include sbt_comm012_global

var
    GrindHeadId                          : integer
    i                                   : integer

%include include/lib_inst_h
%include include/lib_tp_if_h
%include include/lib_string_h
%include include/lib_math_h
%include sbt_comm012_h

begin 

    GrindHeadId                              = get_iparm_(1)                 --获取对应打磨头ID

    if (GrindHeadId <= 0) or (GrindHeadId > MAX_GRINDNUM) then 
        post_err(CC_UALARM, 'InvalidGrindHead.[ID:' + int_to_str_(GrindHeadId) + ']', 0, CC_ABORT)
    endif
    if (get_iparm_(2) < 0) or (get_iparm_(2) > $scr.$maxnumutool) then 
        post_err(CC_UALARM, 'InvalidToolNum.[ID:' + int_to_str_(GrindHeadId) + ',T:' + int_to_str_(get_iparm_(2)) + ']', 0, CC_ABORT)
    endif
    if (get_iparm_(3) < 0) or (get_iparm_(3) > $max_dig_prt) then 
        post_err(CC_UALARM, 'InvalidDOAdr[ID:' + int_to_str_(GrindHeadId) + ',ADR:' + int_to_str_(get_iparm_(3)) + ']', 0, CC_ABORT)
    endif
    if (get_iparm_(4) < 0) or (get_iparm_(4) > $max_dig_prt) then 
        post_err(CC_UALARM, 'InvalidDIAdr[ID:' + int_to_str_(GrindHeadId) + ',ADR:' + int_to_str_(get_iparm_(4)) + ']', 0, CC_ABORT)
    endif
    if (get_rparm_(5) < 0.1) or (get_rparm_(5) > 6553.5) then 
        post_err(CC_UALARM, 'InvalidGrindSpd[ID:' + int_to_str_(GrindHeadId) + ',S:' + rel_to_str_(get_rparm_(5)) + ']', 0, CC_ABORT)
    endif

    HeadChkInit

    GrindBlock.HeadConfig[GrindHeadId].BindToolNum    = get_iparm_(2)                 --获取对应打磨头输出绑定起始地址
    GrindBlock.HeadConfig[GrindHeadId].DoutputAddr    = get_iparm_(3)                 --获取对应打磨头输出绑定起始地址
    GrindBlock.HeadConfig[GrindHeadId].DinputAddr     = get_iparm_(4)                 --获取对应打磨头输入绑定起始地址
    GrindBlock.HeadConfig[GrindHeadId].MaxGrindSped   = get_rparm_(5)                 --获取对应打磨头最大速度 
    GrindBlock.HeadConfig[GrindHeadId].HeadEnable     = int_to_bol_(get_iparm_(6))    --获取对应打磨头是否启用

    HeadStatus(GrindHeadId)
end head_init

